import org.gradle.api.publish.maven.MavenPublication

configurations {
  shadedApi // A configuration that will be on the compile classpath only, and exposed to dependencies
            // This prevents it from being picked up by shadowJar tasks.

  apiElements.extendsFrom(shadedApi)
  compileOnly.extendsFrom(shadedApi)
}

dependencies {
  shadedApi 'us.myles:viaversion-common:3.0.0'
  implementation("net.kyori:adventure-text-serializer-gson:${rootProject.adventure}") {
    exclude group: "com.google.code.gson"
  }
  implementation project(':adventure-platform-facet')
  compileOnly "io.netty:netty-all:4.0.20.Final"
}

publishing {
  publications {
    it.named("maven", MavenPublication.class).configure {
      // Apply dependency hack for maven consumers
      pom.withXml() {
        asNode().dependencies.'*'.findAll() {
          it.groupId.text() == 'us.myles'
        }.each() {
          it.scope*.value = 'provided'
        }
      }
    }
  }
}