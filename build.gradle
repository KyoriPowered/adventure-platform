buildscript {
  ext {
    indraVersion = '3.0.1'
  }
}

plugins {
  id 'net.kyori.indra' version "$indraVersion" apply false
  id 'net.kyori.indra.publishing.sonatype' version "$indraVersion"
  id 'net.kyori.indra.licenser.spotless' version "$indraVersion" apply false
  id 'net.kyori.indra.crossdoc' version "$indraVersion" apply false
}

// Adventure version
ext.adventure = "4.13.0"

group 'net.kyori'
version '4.3.1-SNAPSHOT'
description 'Legacy platform integrations for the adventure UI library'

indraSonatype {
  useAlternateSonatypeOSSHost("s01")
}

/**
 * Bukkit and its dependencies have metadata that is incorrect and excludes some annotation APIs.
 *
 * To suppress compiler warnings from being unable to find the appropriate annotations,
 * we inject the dependencies here.
 */
@CacheableRule
class BukkitAnnotationsRule implements ComponentMetadataRule {

  @Override
  void execute(final ComponentMetadataContext componentMetadataContext) {
    componentMetadataContext.details.withVariant("compile") {
      withDependencies {
        add("com.google.errorprone:error_prone_annotations:2.18.0")
        add("org.jetbrains:annotations:24.0.1")
      }
    }
  }
}

subprojects {
  apply plugin: 'net.kyori.indra'
  apply plugin: 'net.kyori.indra.publishing'
  apply plugin: 'net.kyori.indra.checkstyle'
  apply plugin: 'net.kyori.indra.licenser.spotless'
  apply plugin: 'net.kyori.indra.crossdoc'

  dependencies {
    components {
      withModule("com.destroystokyo.paper:paper-api", BukkitAnnotationsRule)
    }
    annotationProcessor 'ca.stellardrift:contract-validator:1.0.1'
    checkstyle 'ca.stellardrift:stylecheck:0.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
  }
  
  tasks.withType(Javadoc).configureEach {
    options {
      links(
        "https://jd.advntr.dev/api/$adventure",
        "https://jd.advntr.dev/text-serializer-legacy/$adventure",
        "https://jd.advntr.dev/text-serializer-gson/$adventure",
        "https://jd.advntr.dev/nbt/$adventure"
      )
    }
  }

  indra {
    javaVersions {
      testWith(8, 11, 17)
    }

    github("KyoriPowered", "adventure-platform") {
      ci(true)
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = 'kashike'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'lucko'
            name = 'Luck'
            url = 'https://lucko.me'
            email = 'git@lucko.me'
          }

          developer {
            id = 'zml'
            name = 'zml'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'Electroid'
          }
        }
      }
    }
  }
  
  indraCrossdoc {
      baseUrl().set(providers.gradleProperty('javadocPublishRoot'))
      nameBasedDocumentationUrlProvider {
          projectNamePrefix = "adventure-platform-"
      }
  }
}
