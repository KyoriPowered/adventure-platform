import org.gradle.api.initialization.resolve.RepositoriesMode

plugins {
  id "org.gradle.toolchains.foojay-resolver-convention" version "0.9.0"
}

rootProject.name = 'adventure-platform-parent'

dependencyResolutionManagement {
  repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
  repositories {
    mavenCentral()
    maven {
      name = "sonatypeSnapshots"
      url = "https://oss.sonatype.org/content/repositories/snapshots/"
      mavenContent { snapshotsOnly() }
    }
    maven {
      name = "sponge"
      url = 'https://repo.spongepowered.org/repository/maven-public/'
      mavenContent { includeGroup "org.spongepowered" }
    }
    maven {
      name = 'viaversion'
      url = 'https://repo.viaversion.com'
      mavenContent { includeGroup "com.viaversion" }
    }
    maven {
      name = "papermc"
      url = 'https://repo.papermc.io/repository/maven-public/'
      mavenContent {
        includeGroup "io.papermc.paper"
        includeGroup "com.destroystokyo.paper"
      }
    }
    maven {
      name = "minecraft-libraries"
      url = "https://libraries.minecraft.net"
      mavenContent {
        includeGroup "com.mojang"
      }
    }
  }
}

['bungeecord', 'legacy-text3'].each {
  include "text-serializer-$it"
  findProject(":text-serializer-$it")?.name = "adventure-text-serializer-$it"
}

include 'api'
findProject(':api')?.name = 'adventure-platform-api'

['facet', 'viaversion', 'bukkit', 'bungeecord', 'spongeapi'].each {
  include "platform-$it"
  findProject(":platform-$it")?.name = "adventure-platform-$it"
}
